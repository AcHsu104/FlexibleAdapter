apply plugin: 'com.github.dcendents.android-maven'

if (gradle.getStartParameter().getTaskNames().contains("install")) {
    println("> Installing: " + libraryName + " v" + versioning.name)
}

group = publishedGroupId // Maven Group ID for the artifact

// Create a task to generate a sources JAR
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Create a task to generate Javadoc
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = files(android.getBootClasspath().join(File.pathSeparator))
}

// Create a task to generate a Javadoc JAR, depends on the Javadoc task
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.getDestinationDir()
}

// Register the tasks so they can be used as dependencies
artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging packageExt
                groupId publishedGroupId
                artifactId artifact
                version versioning.name

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}