apply plugin: 'com.jfrog.artifactory'

// Create a task to generate a sources JAR
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Create a task to generate Javadoc
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = files(android.getBootClasspath().join(File.pathSeparator))
}

// Create a task to generate a Javadoc JAR, depends on the Javadoc task
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.getDestinationDir()
}

// Register the tasks so they can be used as dependencies
artifacts {
    archives sourcesJar
    archives javadocJar
}

artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory/'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = versioning.name.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
            username = getProperties().get("bintray.user")
            password = getProperties().get("bintray.apikey")
        }
        //noinspection GroovyAssignabilityCheck
        defaults {
            publishArtifacts = true
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}